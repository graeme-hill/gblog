title: LINQ to SQL Gotcha #5: Column Default Values
labels: .NET, LINQ, LINQ to SQL, SQL Server, VB.NET

A common pattern in database design is to use make a column required, give it a default value and then never think about it when doing <code>INSERT</code>s.  A perfect example would be a <code>createdDate</code> column on the <code>Users</code> with a default value of <code>GetDate()</code>.  Here's the full table definition<!--break-->:

<ul>
<li>userID (identity key)</li>
<li>userName</li>
<li>password</li>
<li>ts (timestamp)</li>
<li>createdDate (default value = GetDate())</li>
</ul>

In this case we can easily insert into the table without worrying about the <code>createdDate</code>, <code>userID</code>, or <code>ts</code> columns:

    INSERT INTO Users (userName, password) VALUES ('asdf', 'qwer')

However, since this is the 21st century, we don't want to do this in SQL, we want to do it with an ORM.  Unfortunately, LINQ to SQL doesn't do a very good job with this.

    Using context = New TestDataContext

        ' Output SQL to the console for debugging
        context.Log = Console.Out

        ' Attach a new user and submit the changes
        Dim newUser As New User With {.userName = "NewUser", .password = "password"}
        context.Users.InsertOnSubmit(newUser)
        context.SubmitChanges()

    End Using

The above code generates the following <code>INSERT</code> statement when <code>SubmitChanges()</code> is called (note: I replaced <code>@p0</code>, <code>@p1</code>, etc with their actual values to make the query more readable):

    INSERT INTO [dbo].[Users]([userName], [password], [createdDate]) VALUES ('NewUser', 'password', NULL)

This query fails and we get a <code>SqlTypeException</code> because <code>createdDate</code> is <code>NOT NULL</code> and <code>NULL</code> cannot be converted to a valid date.  Notice that the generated SQL does not attempt to explicitly set a value for <code>userID</code> or <code>ts</code>.  It appears that LINQ to SQL knows how to deal with <code>IDENTITY</code> fields and <code>TIMESTAMP</code>s, but not how to deal with other required columns that happen to have a default value.

I would have expected LINQ to SQL to generate a query that does not explicitly set <code>createdDate</code> so that SQL Server could handle it, but no such luck.  You can easily set the <code>createdDate</code> manually like this:

    Dim newUser As New User With {.userName = "NewUser", .password = "password", .createdDate = Date.Now}

It really sucks to have to do this every time though, especially if you have many fields to fill in.  A possible alternative is to put a partial class on either your <code>DataContext</code> or just on the <code>User</code> class and write some code that will automatically initialize fields like <code>createdDate</code>.   If you want to make generic behaviour for this (eg: automatically set columns named "createdDate" to <code>Date.Now</code> when <code>SubmitChanges</code> is called) you can do something like this in the <code>DataContext</code> partial class:

    Public Overrides Sub SubmitChanges(ByVal failureMode As ConflictMode)

        ' NOTE: this is just a sample to get you started
        For Each insert In GetChangeSet().Inserts
            Dim createdDateProp = insert.GetType.GetProperty("createdDate")
            If createdDateProp IsNot Nothing Then
                createdDateProp.SetValue(insert, Date.Now, Nothing)
            End If
        Next

        MyBase.SubmitChanges(failureMode)

    End Sub
