<!DOCTYPE html>
<html>
	<head>
		<title>Finding all validation errors on an IDataErrorInfo object</title>
		<link rel="stylesheet" type="text/css" href="/resources/css/style.css">
	</head>

	<body>
		<div id="div_header">
			<h1><a href="/">Graeme Hill's Dev Blog</a></h1>
		</div>

		<div id="div_content">
			<div class="div_article">
	<div class="div_article_header">
		<h1><a href="/finding-all-validation-errors-on-an-idataerrorinfo-object">Finding all validation errors on an IDataErrorInfo object</a></h1>
		<p>2009.300</p>
	</div>
	<div class="div_article_content">
		<p>title: Finding all validation errors on an IDataErrorInfo object
labels: .NET, VB.NET, WPF</p>

<p>WPF data binding has built in support for <code>IDataErrorInfo</code>, so it is easy to display a validation error when a property has invalid data.  However, sometimes we need to manually find all the validation errors on an object.  A perfect example of this is when trying to save data.  Often you will want to verify that there are no validation errors before allowing the save operation to proceed.  Using the MVVM pattern it would be ideal to determine whether there are any errors purely within your model view.  The default behavior of <code>IDataErrorInfo</code> does not give a collection of all current errors.  Instead, it will just tell you if a given property has an error (ie: using <code>IDataErrorInfo.Item</code>), so all we have to do to find all the errors is enumerate through each property on the class and call the <code>Item</code> property with that property name as the argument.  The <code>GetValidationErrors</code> function below does exactly that<!--break-->:</p>

<pre><code>Option Strict On

Imports System.ComponentModel

Public Class ValidationHelper

    ''' &lt;summary&gt;
    ''' Checks for errors in &lt;c&gt;validatable&lt;/c&gt; and returns all the errors found.
    ''' &lt;/summary&gt;
    Public Shared Function GetValidationErrors(ByVal validatable As IDataErrorInfo) As IEnumerable(Of DataError)

        Dim errors As New List(Of DataError)

        ' Iterate through every property in the class
        For Each prop In validatable.GetType.GetProperties

            ' If the property has an error, then add it to the list
            Dim errorMessage = validatable(prop.Name)
            If errorMessage IsNot Nothing Then
                errors.Add(New DataError(prop.Name, errorMessage))
            End If

        Next

        Return errors

    End Function

    ''' &lt;summary&gt;
    ''' Represents a single error.  It's a propertyName, errorMessage pair
    ''' &lt;/summary&gt;
    Public Class DataError

        Private _propertyName As String
        Private _errorMessage As String

        Public Sub New(ByVal propertyName As String, ByVal errorMessage As String)
            _propertyName = propertyName
            _errorMessage = errorMessage
        End Sub

        ''' &lt;summary&gt;
        ''' The name of the property that has the error.
        ''' &lt;/summary&gt;
        Public ReadOnly Property PropertyName() As String
            Get
                Return _propertyName
            End Get
        End Property

        ''' &lt;summary&gt;
        ''' A description of the property's error.
        ''' &lt;/summary&gt;
        Public ReadOnly Property ErrorMessage() As String
            Get
                Return _errorMessage
            End Get
        End Property

    End Class

End Class
</code></pre>

	</div>
</div>
		</div>
	</body>
</html>